apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  productFlavors {}
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

  implementation "com.android.support:appcompat-v7:$support_version"

  // Tests
  testImplementation 'junit:junit:4.12'
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

publish {
  repoName = 'libs-snapshot-local'
  groupId = 'com.slelyuk.android'
  artifactId = 'circle-avatar'
  publishVersion = "$rootProject.ext.publishVersion"
  desc = ''
  website = 'https://github.com/slelyuk/circle-avatar-view'
  bintrayUser = "$System.env.BINTRAY_USERNAME"
  bintrayKey = "$System.env.BINTRAY_PASSWORD"
  dryRun = false
}
